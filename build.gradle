plugins {
    id "base"
    id "com.github.spotbugs" version "4.0.5"
    id "de.undercouch.download" version "4.0.4"
    id 'net.researchgate.release' version '2.6.0'
}

description = 'Ballerinax - DataMapper Extension'

ext.puppycrawlCheckstyleVersion = "8.18"
ext.ballerinaLangVersion = project.ballerinaLangVersion
ext.dataMapperVersion = project.version

allprojects {
    repositories {
        mavenLocal()
        maven {
            url = 'https://maven.wso2.org/nexus/content/repositories/releases/'
        }

        maven {
            url = 'https://maven.wso2.org/nexus/content/repositories/snapshots/'
        }

        maven {
            url = 'https://maven.wso2.org/nexus/content/groups/wso2-public/'
        }

        maven {
            url = 'https://repo.maven.apache.org/maven2'
        }

        maven {
            url = 'https://maven.pkg.github.com/ballerina-platform/module-ballerina-io'
            credentials {
                username System.getenv("packageUser")
                password System.getenv("packagePAT")
            }
        }
        
        /* Ballerina lang staging repository */
        maven {
            url = 'https://maven.wso2.org/nexus/content/repositories/orgballerinalang-1590/'
        }
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    configurations {
        ballerinaStdLibs
    }

    dependencies {
        /* Standard libraries */
        ballerinaStdLibs "org.ballerinalang:io-ballerina:${stdlibIoVersion}"
    }

    publishing {
        repositories {
            maven {
                if (project.version.endsWith('-SNAPSHOT')) {
                    url "https://maven.wso2.org/nexus/content/repositories/snapshots/"
                } else {
                    url "https://maven.wso2.org/nexus/service/local/staging/deploy/maven2/"
                }
                credentials {
                    username System.getenv("NEXUS_USERNAME")
                    password System.getenv("NEXUS_PASSWORD")
                }
            }
        }
    }

    release {
        // Disable check snapshots temporarily
        buildTasks = []
        failOnSnapshotDependencies = false
        failOnCommitNeeded = false
        versionPropertyFile = 'gradle.properties'
        tagTemplate = 'v${version}'
        git {
            // To release from any branch
            requireBranch = null
        }
    }

    afterReleaseBuild.dependsOn publishToMavenLocal
    afterReleaseBuild.dependsOn publish
}

apply from: "gradle/javaProject.gradle"
